apply from: 'gradle/deps.gradle'

buildscript {
  ext {
    nexusPlugin = '2.3.1'
    sonarPlugin = '3.0'
    aptPlugin = '3.24.0'
  }

  repositories {
    mavenCentral()
    jcenter()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  dependencies {
    classpath "com.bmuschko:gradle-nexus-plugin:${nexusPlugin}"
    classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:${sonarPlugin}"
    classpath "com.diffplug.gradle:goomph:${aptPlugin}"
  }
}

apply plugin: 'org.sonarqube'

allprojects {
  apply plugin: 'java'
  apply plugin: 'java-library'
  apply plugin: 'jacoco'
  apply plugin: 'com.bmuschko.nexus'
  apply plugin: 'com.diffplug.eclipse.apt'

  group = 'com.github.tonivade'
  version = '2.1.1'

  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'

  repositories {
    mavenLocal()
    mavenCentral()
    maven {
      url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
  }

  jacoco {
    toolVersion = '0.8.5'
  }

  compileJava {
    options.compilerArgs << '-Xlint:unchecked'
    options.compilerArgs << '-Xlint:rawtypes'
    options.release = 8
  }

  compileTestJava {
    options.compilerArgs << '-Xlint:unchecked'
    options.compilerArgs << '-Xlint:rawtypes'
    options.release = 8
  }

  jacocoTestReport {
    reports {
      xml.enabled = true
      html.enabled = true
      xml.destination file("${buildDir}/reports/jacoco/report.xml")
    }
  }
}

subprojects {
  test {
    useJUnitPlatform()

    jacoco {
      destinationFile = file("${buildDir}/jacoco/test.exec")
    }

    reports.html.enabled = false
  }

  modifyPom {
    project {
      name 'Pure Fun'
      description 'Functional Programming Library for Java'
      url 'https://github.com/tonivade/purefun'
      inceptionYear '2018'

      scm {
        url 'https://github.com/tonivade/purefun'
        connection 'scm:https://github.com/tonivade/purefun.git'
        developerConnection 'scm:git://github.com/tonivade/purefun.git'
      }

      licenses {
        license {
          name 'The MIT License (MIT)'
          url 'https://github.com/tonivade/purefun/blob/master/LICENSE'
          distribution 'repo'
        }
      }

      developers {
        developer {
          id 'tonivade'
          name 'Antonio MuÃ±oz'
          email 'antoniogmc@gmail.com'
        }
      }
    }
  }

  extraArchive {
    sources = true
    tests = false
    javadoc = true
  }

  nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
  }

  dependencies {
    testImplementation libs.junitJupiter
    testRuntime libs.junitRunner
    testImplementation libs.equalsVerifier
    testImplementation libs.mockitoCore
    testImplementation libs.mockitoJunit
  }
}

def publishedProjects = subprojects.findAll()

task jacocoFullReport(type: JacocoReport, group: 'Coverage reports', dependsOn: jacocoTestReport) {
  description = 'Generates an aggregate report from all subprojects'

  additionalSourceDirs.from = files(publishedProjects.sourceSets.main.allSource.srcDirs)
  sourceDirectories.from = files(publishedProjects.sourceSets.main.allSource.srcDirs)
  classDirectories.from = files(publishedProjects.sourceSets.main.output)
  executionData.from = files(publishedProjects.jacocoTestReport.executionData).findAll { it.exists() }

  reports {
    html.enabled = true
    xml.enabled = true
    xml.destination file("${buildDir}/reports/jacoco/report.xml")
  }
}

task fullTestReport(type: TestReport, group: 'Html Report with all the tests', dependsOn: check) {
  destinationDir = file("$buildDir/reports/allTests")

  reportOn subprojects*.test
}
